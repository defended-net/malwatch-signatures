/*             __            __      __
   __ _  ___ _/ /    _____ _/ /_____/ /
  /    \/ _ `/ / |/|/ / _ `/ __/ __/ _ \
 /_/_/_/\_,_/_/|__,__/\_,_/\__/\__/_//_/
                            defended.net
*/

rule ws_php_exec_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "webshell exec"

  strings:
    $s1 = "<?php"
    $s2 = "$_POST"
    $s3 = "$_GET"
    $s4 = "<form"
    $r1 = /(^|shell_|[^\w])exec\s?\(/
    $r2 = /(^|[^\w])proc_open\s?\(/
    $r3 = /(^|[^\w])popen\s?\(/
    $r4 = /(^|[^\w])passthru\s?\(/
    $r5 = /(^|[^\w])system\s?\(\s?[\$\'\"]/
    $r6 = /(^|[^\w])eval\s?\(/

  condition:
    $s1 in (0..8) and
    any of ($s2, $s3, $s4) and
    any of ($r*)
}

rule ws_php_proxy_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "webshell proxy"

  strings:
    $s1 = "socket_accept"
    $s2 = "socket_bind"
    $r1 = /(^|shell_|[^\w])exec\s?\(/
    $r2 = /(^|[^\w])proc_open\s?\(/
    $r3 = /(^|[^\w])popen\s?\(/
    $r4 = /(^|[^\w])passthru\s?\(/
    $r5 = /(^|[^\w])system\s?\(\s?[\$\'\"]/
    $r6 = /(^|[^\w])eval\s?\(/

  condition:
    any of ($s1, $s2) and
    any of ($r*)
}
