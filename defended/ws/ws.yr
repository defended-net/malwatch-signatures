/*             __            __      __
   __ _  ___ _/ /    _____ _/ /_____/ /
  /    \/ _ `/ / |/|/ / _ `/ __/ __/ _ \
 /_/_/_/\_,_/_/|__,__/\_,_/\__/\__/_//_/
                            defended.net
*/

rule ws_php_exec_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "webshell exec"

  strings:
    $s1 = "<?php"
    $s2 = "$_POST"
    $s3 = "$_GET"
    $s4 = "<form" nocase
    $s5 = "socket_accept" nocase
    $s6 = "socket_bind" nocase
    $r1 = /(^|shell_|[^\w:])exec[ \t]{,4}\(/ nocase
    $r2 = /(^|[^\w])proc_open[ \t]{,4}\(/ nocase
    $r3 = /(^|[^\w])popen[ \t]{,4}\(/ nocase
    $r4 = /(^|[^\w])passthru[ \t]{,4}\(/ nocase
    $r5 = /(^|[^\w])system[ \t]{,4}\(\s?[\$\'\"]/ nocase
    $r6 = /(^|[^\w])eval[ \t]{,4}\(/ nocase

  condition:
    $s1 and
    any of ($s2, $s3, $s4, $s5, $s6) and
    any of ($r*)
}