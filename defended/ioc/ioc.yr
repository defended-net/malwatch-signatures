/*             __            __      __
   __ _  ___ _/ /    _____ _/ /_____/ /
  /    \/ _ `/ / |/|/ / _ `/ __/ __/ _ \
 /_/_/_/\_,_/_/|__,__/\_,_/\__/\__/_//_/
                            defended.net
*/

rule ioc_php_obf_comments_0 {
  meta:
    author = "roscoe skeens"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obfuscation using comments."

  strings:
    // many js false positives.
    $tag = "<?php"
    $re = /\/\*\S{5,25}\*\//

  condition:
    $tag at 0 and
    #re > 1 and
    @re[2] - (@re[1] + !re[1]) <= 64
}