/*             __            __      __
   __ _  ___ _/ /    _____ _/ /_____/ /
  /    \/ _ `/ / |/|/ / _ `/ __/ __/ _ \
 /_/_/_/\_,_/_/|__,__/\_,_/\__/\__/_//_/
                            defended.net
*/

rule ioc_php_obf_comments_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf comments"

  strings:
    $r1 = /\/\*\S{1,25}\*\//
    $s1 = "<?php"

  condition:
    $s1 in (0..8) and
    #r1 > 1 and
    @r1[2] - (@r1[1] + !r1[1]) <= 64
}

rule ioc_php_obf_comments_1 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf comments"

  strings:
    $h1 = { (0D 0A | 0A) 2E 20[0-10] (2F 2F | 2F 2A) }

  condition:
    $h1
}

rule ioc_php_obf_hex_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf hex"

  strings:
    $r1 = /\\x[0-9a-fA-F]{2}\\\d{2,3}/
    $s1 = "<?php"

  condition:
    $s1 in (0..8) and
    $r1
}

rule ioc_php_obf_chevrons_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf chevrons"

  strings:
    $r1 = /(<[\da-fA-F]{2}>){2}/

  condition:
    $r1
}

rule ioc_php_obf_octals_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf octals"

  strings:
    $r1 = /\\[0-7]{3}/
    $s1 = "<?php"
    $s2 = "preg_match"
    $s3 = "\\000-"

  condition:
    $s1 in (0..8) and
    #r1 > 2 and
    @r1[2] - (@r1[1] + !r1[1]) <= 12 and
    not any of ($s2, $s3)
}

rule ioc_php_obf_concats_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf concats"

  strings:
    $r1 = /\$\w{1,10}\{\d{1,2}\}\./

  condition:
    #r1 > 3
}

rule ioc_php_obf_concats_incr_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf concats incr"

  strings:
    $r1 = /(\$\w{,8}\+\+;){2}/

  condition:
    $r1
}

rule ioc_php_obf_concats_name_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf concats name"

  strings:
    $r1 = /(\s{,4}\.\s{,4}['"][a-zA-Z0-9_]{1,16}['"]){3}/

  condition:
    $r1
}

rule ioc_php_obf_scoped_sglobal_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf scoped sglobal"

  strings:
    $h1 = { 24 5F 53 45 52 56 45 52 [0-10] 5B [0-10] 5F [0-8] 3A 3A }

  condition:
    $h1
}

rule ioc_php_obf_xor_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf xor"

  strings:
    $s1 = "<?php"
    $r1 = /\beval[ \t]{0,8}\([^)]/
    $r2 = /\bstrrev/
    $r3 = /\bstr_rot13/
    $r4 = /\bhex2bin/
    $r5 = /\bconvert_uudecode/

  condition:
    $s1 in (0..8) and
    $r1 and
    any of ($r2, $r3, $r4, $r5)
}

rule ioc_php_ip_fget_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "ip fget"

  strings:
    $r1 = /file_get_contents\s{,4}\(\s{,4}"https?:\/\/(\d{1,3}\.){3}/

  condition:
    $r1
}

rule ioc_php_dropper_small_upload_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "dropper small upload"

  strings:
    $s1 = "$_POST"
    $s2 = "$_GET"
    $s3 = "$_REQUEST"
    $s4 = "$_FILES"
    $s5 = "<form" nocase
    $s6 = "file_get_contents" nocase
    $s7 = "file_put_contents" nocase
    $s8 = "move_uploaded_file" nocase
    $s9 = "fwrite" nocase

  condition:
    any of ($s1, $s2, $s3, $s4, $s5, $s6) and
    any of ($s7, $s8, $s9) and
    filesize < 1024
}

rule ioc_php_artifact_sys_cfg_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "artifcat sys cfg"

  strings:
    $s1 = "<?php"
    $s2 = "/etc/passwd"
    $s3 = "/etc/hosts"
    $s4 = "/etc/named.conf"

  condition:
    $s1 in (0..8) and
    any of ($s2, $s3, $s4)
}

rule ioc_js_obf_b64_redir_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf b64 redir"

  strings:
    $s1 = "d2luZG93LmxvY2F0aW9u"

  condition:
    $s1
}

rule ioc_js_obf_b64_inj_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "obf b64 inj"

  strings:
    $s1 = "ZG9jdW1lbnQud3JpdGU"

  condition:
    $s1
}