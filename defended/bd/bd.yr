/*             __            __      __
   __ _  ___ _/ /    _____ _/ /_____/ /
  /    \/ _ `/ / |/|/ / _ `/ __/ __/ _ \
 /_/_/_/\_,_/_/|__,__/\_,_/\__/\__/_//_/
                            defended.net
*/

rule bd_php_anonfox_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "eval hex"

  strings:
    $h1 = { 68 65 78 64 65 63 [0-10] 28 [0-10] 73 75 62 73 74 72 }
    $h2 = { 73 74 72 6C 65 6E [0-10] 28 [0-10] 74 72 69 6D }
    $h3 = { 70 61 63 6B [0-10] 28 [0-10] 22 [0-10] ?? [0-10] 22 }

  condition:
    all of them
}

rule bd_php_eval_gz_b64_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "eval gz b64 hex"

  strings:
    $s1 = "6576616C28677A756E636F6D7072657373286261736536345F6465636F64652827" fullword ascii

  condition:
    $s1
}

rule bd_php_rce_stream_sglobal_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "rce stream sglobal"

  strings:
    $s1 = "$_GET"
    $s2 = "stream_wrapper_register"
    $s3 = "fopen"

  condition:
    all of them
}

rule bd_php_rce_assert_sglobal_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "rce assert sglobal"

  strings:
    $h1 = { 61 73 73 65 72 74 [0-10] 28 [0-10] 24 5F }

  condition:
    $h1
}

rule bd_php_rce_cback_sglobal_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "rce cback sglobal"

  strings:
    $r1 = /(array_(filter|walk|udiff)|(u|ua|uk)sort)[ \t]{,8}\([ \t]{,8}\[\$(GLOBALS|_)/
    $r2 = /(call_user_func|register_shutdown_function)[ \t]{,8}\(.{1,16}\[?\$(GLOBALS|_)[CFGPRS]/

  condition:
    any of them
}

rule bd_php_rce_bticks_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "rce bticks"

  strings:
    $r1 = /`[ \t]{0,8}\$/

  condition:
    $r1 and filesize < 512
}

rule bd_php_cback_xpath_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "cback xpath"

  strings:
    $s1 = "php:functionString"

  condition:
    $s1
}

rule bd_php_cback_xslt_0 {
  meta:
    author = "roscoe skeens (defended.net)"
    license = "https://creativecommons.org/licenses/by-nc-sa/4.0"
    description = "cback xslt"

  strings:
    $h1 = { 2D 3E [0-10] 72 65 67 69 73 74 65 72 50 48 50 46 75 6E 63 74 69 6F 6E 73 }

  condition:
    $h1
}